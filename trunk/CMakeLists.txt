# # # # # #  project setup  # # # # # #
CMAKE_MINIMUM_REQUIRED( VERSION 2.8 )
PROJECT( osgBullet )

SET( CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/CMakeModules;${CMAKE_MODULE_PATH}" )

SET( LIBRARY_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin CACHE PATH "Single directory for all libraries." )
MAKE_DIRECTORY( ${LIBRARY_OUTPUT_PATH} )
MARK_AS_ADVANCED( LIBRARY_OUTPUT_PATH )

SET( EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin CACHE PATH "Single directory for all executables." )
MAKE_DIRECTORY( ${EXECUTABLE_OUTPUT_PATH} )
MARK_AS_ADVANCED( EXECUTABLE_OUTPUT_PATH )

IF( WIN32 AND MSVC )
    OPTION( OSGBULLET_WIN32_USE_MP "Build with multiple processes (/MP option; VS 2005 and above)." ON )
    MARK_AS_ADVANCED( OSGBULLET_WIN32_USE_MP )
    IF( OSGBULLET_WIN32_USE_MP )
        SET( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /MP" )
    ENDIF( OSGBULLET_WIN32_USE_MP )
ENDIF( WIN32 AND MSVC )

INCLUDE( CMakeMacros )

FIND_PACKAGE( OSG REQUIRED )
FIND_PACKAGE( OSGWorks REQUIRED )
FIND_PACKAGE( Bullet REQUIRED )

# add bullet & osg for entire project for include & lib paths
INCLUDE_DIRECTORIES( 
    ${PROJECT_SOURCE_DIR}/include
    ${OSG_INCLUDE_DIRS}
    ${OSGWORKS_INCLUDE_DIR}
    ${BULLET_INCLUDE_DIR}
    ${BULLET_EXTRAS_INCLUDE_DIR}
)
# HACK: Not sure why this is necessary, but linket can't find osgbBulletd.lib without it.
LINK_DIRECTORIES(
    ${EXECUTABLE_OUTPUT_PATH}/Debug
)

# Possible future support for collision-only (no dynamics)
IF( BULLET_DYNAMICS_FOUND )
    ADD_DEFINITIONS( -DUSE_BULLET_DYNAMICS )
ENDIF( BULLET_DYNAMICS_FOUND )

OPTION( OSGBULLET_USE_DOUBLE_PRECISION "Use Bullet built with double precision" OFF )
IF( OSGBULLET_USE_DOUBLE_PRECISION )
    ADD_DEFINITIONS( -DBT_USE_DOUBLE_PRECISION)
ENDIF( OSGBULLET_USE_DOUBLE_PRECISION )


OPTION( OSGBULLET_BUILD_APPLICATIONS "Enable to build applications" ON )
IF( OSGBULLET_BUILD_APPLICATIONS )
    ADD_SUBDIRECTORY( applications )
ENDIF( OSGBULLET_BUILD_APPLICATIONS )

OPTION( OSGBULLET_BUILD_EXAMPLES "Enable to build example code" ON )
IF( OSGBULLET_BUILD_EXAMPLES )
    ADD_SUBDIRECTORY( examples )
ENDIF( OSGBULLET_BUILD_EXAMPLES )

OPTION( OSGBULLET_BUILD_TESTS "Enable to build test code" OFF )
IF( OSGBULLET_BUILD_TESTS )
    ADD_SUBDIRECTORY( tests )
ENDIF( OSGBULLET_BUILD_TESTS )

ADD_SUBDIRECTORY( src/osgbBullet )
ADD_SUBDIRECTORY( src/plugins )


#
# Doxygen
OPTION( OSGBULLET_DOCUMENTATION "Enable to create build target for Doxygen documentation." OFF )
MARK_AS_ADVANCED( CLEAR OSGBULLET_DOCUMENTATION )
IF( OSGBULLET_DOCUMENTATION )
    FIND_PACKAGE( Doxygen REQUIRED )
    SET( HAVE_DOT "NO" )
    IF( DOXYGEN_DOT_PATH )
        SET( HAVE_DOT "YES" )
    ENDIF( DOXYGEN_DOT_PATH )
    
    CONFIGURE_FILE( ${PROJECT_SOURCE_DIR}/doc/doxyfile.cmake
        ${PROJECT_BINARY_DIR}/doc/doxyfile
    )
    ADD_CUSTOM_TARGET( Documentation ${DOXYGEN_EXECUTABLE}
        ${PROJECT_BINARY_DIR}/doc/doxyfile
    )
ENDIF( OSGBULLET_DOCUMENTATION )
